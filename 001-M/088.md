Curved Raisin Cottonmouth

medium

# AMO minters can borrow collateral exceeds freeCollateralBalance limit

## Summary
when AmoMinters call amoMinterBorrow to borrow, the amount of collateral that can be borrowed should should be less than freeCollateralBalance. However, amoMinterBorrow function doesn't follow this restriction, so AmoMinters could borrow up to all the collateral in Ubiquity pool, including redeemed collateral that waiting for users to collectRedemption. Thusly, this issue may break the redeem then claim process.

## Vulnerability Detail
AmoMinters call amoMinterBorrow to borrow collateral in:
https://github.com/sherlock-audit/2023-12-ubiquity/blob/d9c39e8dfd5601e7e8db2e4b3390e7d8dff42a8e/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibUbiquityPool.sol#L574-L598
amoMinterBorrow didn't check collateralAmount and simply transfer collateral to AmoMinters.
```solidity
    function amoMinterBorrow(uint256 collateralAmount) internal onlyAmoMinter {
        UbiquityPoolStorage storage poolStorage = ubiquityPoolStorage();

        // checks the collateral index of the minter as an additional safety check
        uint256 minterCollateralIndex = IDollarAmoMinter(msg.sender)
            .collateralIndex();

        // checks to see if borrowing is paused
        require(
            poolStorage.isBorrowPaused[minterCollateralIndex] == false,
            "Borrowing is paused"
        );

        // ensure collateral is enabled
        require(
            poolStorage.isCollateralEnabled[
                poolStorage.collateralAddresses[minterCollateralIndex]
            ],
            "Collateral disabled"
        );
       
        // @audit no check on collateralAmount, it should be <= freeCollateralBalance(minterCollateralIndex)
        // transfer
        IERC20(poolStorage.collateralAddresses[minterCollateralIndex])
            .safeTransfer(msg.sender, collateralAmount);
    }
```

## Impact
AmoMinters can borrow all the collateral from Ubiquity pool, and make user failed to collect redemption.

## Code Snippet
amoMinterBorrow
https://github.com/sherlock-audit/2023-12-ubiquity/blob/d9c39e8dfd5601e7e8db2e4b3390e7d8dff42a8e/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibUbiquityPool.sol#L574C1-L598C6

PoC
```solidity
function testAmoMinterBorrow_BorrowExceedsLimit() public {
    vm.prank(admin);
    ubiquityPoolFacet.setPriceThresholds(
        1000000, // mint threshold
        1000000 // redeem threshold
    );

    // user sends 100 collateral tokens and gets 99 Dollars (-1% mint fee)
    vm.prank(user);
    ubiquityPoolFacet.mintDollar(
        0, // collateral index
        100e18, // Dollar amount
        99e18, // min amount of Dollars to mint
        100e18 // max collateral to send
    );

    // user redeems 99 Dollars for collateral
    vm.prank(user);
    ubiquityPoolFacet.redeemDollar(
        0, // collateral index
        99e18, // Dollar amount
        90e18 // min collateral out
    );

    // wait 3 blocks for collecting redemption to become active
    vm.roll(3);

    vm.prank(address(dollarAmoMinter));
    // free collaternal balance that amoMinter could borrow should be less than 100e18
    assertTrue(ubiquityPoolFacet.freeCollateralBalance(0) < 100e18);
    vm.prank(address(dollarAmoMinter));
    ubiquityPoolFacet.amoMinterBorrow(100e18);

    // dollarAmoMinter borrows exceeds free collaternal balance
    assertEq(collateralToken.balanceOf(address(ubiquityPoolFacet)), 0);
    assertEq(collateralToken.balanceOf(address(dollarAmoMinter)), 100e18);
    vm.prank(user);
    // user failed to collect redemption
    vm.expectRevert("ERC20: transfer amount exceeds balance");
    ubiquityPoolFacet.collectRedemption(0);
}
```

## Tool used

Manual Review
Foundry

## Recommendation
Add code for amoMinterBorrow to ensure collateralAmount to be borrowed be less than freeCollateralBalance 