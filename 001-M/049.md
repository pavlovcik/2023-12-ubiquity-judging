Cheesy Maroon Panda

medium

# Users might need to wait an unforeseeable amount of time in order to collect their redemption

## Summary
Users might need to wait an unforeseeable amount of time in order to collect their redemption.
## Vulnerability Detail
Users that want to burn their Ubiquity Dollars and redeem collateral undergo a 2-step process:

1. Call `LibUbiquityPool#redeemDollar()` which stores the user's redeem request and *burns their Ubiquity Dollars*.
2. Call `LibUbiquityPool#collectRedemption()` which sets their redeem request to fulfilled (0) and then transfers the collateral to the user.

**One thing I want to note here is that once a redeem request is made, there is no way to cancel it and go back.** 

Now let's examine the `LibUbiquityPool#amoMinterBorrow()` function in the protocol, I quote the comments:

>"Allows AMO minters to borrow collateral to make yield in external protocols like Compound, Curve, erc..."

https://github.com/sherlock-audit/2023-12-ubiquity/blob/d9c39e8dfd5601e7e8db2e4b3390e7d8dff42a8e/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibUbiquityPool.sol#L568-L598

```javascript
    /**
     * @notice Allows AMO minters to borrow collateral to make yield in external
     * protocols like Compound, Curve, erc...
     * @dev Bypasses the gassy mint->redeem cycle for AMOs to borrow collateral
     * @param collateralAmount Amount of collateral to borrow
     */
    function amoMinterBorrow(uint256 collateralAmount) internal onlyAmoMinter {
        UbiquityPoolStorage storage poolStorage = ubiquityPoolStorage();

        // checks the collateral index of the minter as an additional safety check
        uint256 minterCollateralIndex = IDollarAmoMinter(msg.sender)
            .collateralIndex();

        // checks to see if borrowing is paused
        require(
            poolStorage.isBorrowPaused[minterCollateralIndex] == false,
            "Borrowing is paused"
        );

        // ensure collateral is enabled
        require(
            poolStorage.isCollateralEnabled[
                poolStorage.collateralAddresses[minterCollateralIndex]
            ],
            "Collateral disabled"
        );

        // transfer
        IERC20(poolStorage.collateralAddresses[minterCollateralIndex])
            .safeTransfer(msg.sender, collateralAmount);
    }
```

This is where the issue arises, let's explore the following scenario (for simplicity's sake I will not account fees):
1. Alice has `1000e18` Ubiquity Dollars and the contract balance has the equivalent `1000e18` collateral tokens 
2. Alice makes a redeem request for the whole amount of `1000e18`
3. Before Alice could actually `collectRedemption()` there is a delay of `n` amount of blocks which need to have passed 
4. The `amoMinter` borrows `900e18` before Alice could collect her redemption, the collateral token balance in the contract is now `100e18`
5. Alice tries to collect her redemption but she will not be able to since there are not enough collateral tokens in the contract
6. Alice has to wait for an unforseeable amount of time until either other users mint dollars and deposit enough collateral in order for her to be able to collect her redemption, or the `AMO Minter` returns the borrow.

Since there is no cancellation of requests, Alice is now forced to wait in order to collect whilst her Ubiquity Dollars are already burned and her funds are inaccessible, she also cannot redeem her Dollars for another type of collateral since there is no functionality to re-request/cancel a request. If her collateral token is not very popular and liquidity in the protocol is low she could be forced to wait for a long time before she can collect.

In the case of users minting and redeeming, this scenario is not possible because `LibUbiquityPool#freeCollateralBalance()` is used and  there is also a variable `poolStorage.unclaimedPoolCollateral` which safeguards users from going into a "waiting to collect" state, and thus step 1 would revert, and they would still have their Ubiquity Dollars available, but in the scenario where the `AMO Minter` borrows *after* Alice makes the redemption, but *before* she collects it, her Ubiquity Dollars are already burned and she is left with nothing until she can collect.

One more thing I want to point to are the comments above the function `freeCollateralBalance()`:

https://github.com/sherlock-audit/2023-12-ubiquity/blob/d9c39e8dfd5601e7e8db2e4b3390e7d8dff42a8e/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibUbiquityPool.sol#L263-L276

```javascript
    /**
     * @notice Returns free collateral balance (i.e. that can be borrowed by AMO minters)
     * @param collateralIndex collateral token index
     * @return Amount of free collateral
     */
    function freeCollateralBalance(
        uint256 collateralIndex
    ) internal view returns (uint256) {
        UbiquityPoolStorage storage poolStorage = ubiquityPoolStorage();
        return
            IERC20(poolStorage.collateralAddresses[collateralIndex])
                .balanceOf(address(this))
                .sub(poolStorage.unclaimedPoolCollateral[collateralIndex]);
    }
```

They propose the function is used to decide how much collateral the `AMO Minter` can actually borrow without disrupting the normal functionality of minting/redeeming for users, but that function is never used anywhere inside the `amoMinterBorrow()` function.
## Impact
User cannot take back his collateral for an unforeseeable amount of time (amplified in the case of an unpopular collateral token which has low liquidity in the protocol).
## Code Snippet
https://github.com/sherlock-audit/2023-12-ubiquity/blob/d9c39e8dfd5601e7e8db2e4b3390e7d8dff42a8e/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibUbiquityPool.sol#L568-L598

https://github.com/sherlock-audit/2023-12-ubiquity/blob/d9c39e8dfd5601e7e8db2e4b3390e7d8dff42a8e/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibUbiquityPool.sol#L263-L276
## Tool used
Manual Review
## Recommendation
Allow the `AMO Minter` to only borrow `<=` of `freeCollateralBalance()`.

```diff
    function amoMinterBorrow(uint256 collateralAmount) internal onlyAmoMinter {
        ...
+       require(
+           collateralAmount <= freeCollateralBalance(),
+           "Invalid Amount"
+       );
        
        // transfer
        IERC20(poolStorage.collateralAddresses[minterCollateralIndex])
            .safeTransfer(msg.sender, collateralAmount);
    }
```