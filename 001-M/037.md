Suave Wintergreen Donkey

high

# The absence of a limit in AmoMinterBorrow breaks minting and redeeming and can lead to user losses.

## Summary

The  `amoMinterBorrow` function has no limit to it's maximum withdrawal, making it possible for Automated Market Operators (AMO) to borrow massive amounts of collateral. Depending on the size and time of the borrow, this will have various negative consequences that break the protocol.     

1. If a maximum borrow happens right after  `mintDollar` => `redeemDollar` will fail with "Insufficient pool collateral". User cannot redeem her collateral, but can still swap Dollar on the Curve 3pool.
2. If a maximum borrow happens after  `redeemDollar` but before `collectRedemption` => transfer will fail due to insufficient balance, User has burned her Dollar but cannot redeem collateral. 
3. If a maximum borrow happens in an active market with many minters & redeemers => `freeCollateralBalance` will revert due to underflow, breaking `mintDollar` and stopping anyone from depositing collateral in the protocol.  

Since AMO's are automated dApps that seek maximum yield, they will automatically take the maximum borrow they are allowed in order to maximise revenues. Thus making the above described scenarios a guarantee. 

## Vulnerability Detail

I have created tests to illustrate each scenario. 

All tests should be placed in `ubiquity-dollar/packages/contracts/test/diamond/facets/UbiquityPoolFacet.t.sol`



Scenario 1: Redeeming Dollar for collateral after a amoMinterBorrow.

```solidity
    function testRedeemAfterAmoBorrow_ShouldRevert()
        public
    {
        vm.prank(admin);
        ubiquityPoolFacet.setPriceThresholds(
            1000000, // mint threshold
            1000000 // redeem threshold
        );

        // user sends 100 collateral tokens and gets 99 Dollars (-1% mint fee)
        vm.prank(user);
        ubiquityPoolFacet.mintDollar(
            0, // collateral index
            100e18, // Dollar amount
            99e18, // min amount of Dollars to mint
            100e18 // max collateral to send
        );

        // amoMinter borrows the maximum he is allowed. 
        uint256 balanceContract = MockERC20(collateralToken).balanceOf(address(ubiquityPoolFacet));
        vm.prank(address(dollarAmoMinter));
        ubiquityPoolFacet.amoMinterBorrow(balanceContract);

        // user tries to redeem her dollars, but this reverts since AMO took the entire balance. 
        vm.prank(user);
        vm.expectRevert("Insufficient pool collateral");
        ubiquityPoolFacet.redeemDollar(
            0, // collateral index
            99e18, // Dollar amount
            90e18 // min collateral out
        );
    } 
```
Scenario 2: CollectRedemption with a amoMinterBorrow during the time delay.

```solidity
    function testCollectRedemptionAfterAmoBorrow_ShouldRevert()
        public
    {
        vm.prank(admin);
        ubiquityPoolFacet.setPriceThresholds(
            1000000, // mint threshold
            1000000 // redeem threshold
        );

        // user sends 100 collateral tokens and gets 99 Dollars (-1% mint fee)
        vm.prank(user);
        ubiquityPoolFacet.mintDollar(
            0, // collateral index
            100e18, // Dollar amount
            99e18, // min amount of Dollars to mint
            100e18 // max collateral to send
        );

        // user redeems 99 Dollars for 97.02 (accounts for 2% redemption fee) collateral tokens
        vm.prank(user);

        ubiquityPoolFacet.redeemDollar(
            0, // collateral index
            99e18, // Dollar amount
            90e18 // min collateral out
        );


        // amoMinter borrows the maximum he is allowed.
        uint256 balanceContract = MockERC20(collateralToken).balanceOf(address(ubiquityPoolFacet));
        vm.prank(address(dollarAmoMinter));
        ubiquityPoolFacet.amoMinterBorrow(balanceContract);

        // time delay to collect ends after below roll.
        vm.roll(3);

        // user tries to collect her collateral, but this fails since the balance of the contract is now insufficient. 
        // user has burned her dollar and her collateral is stuck. 
        vm.prank(user);
        vm.expectRevert("ERC20: transfer amount exceeds balance");
        ubiquityPoolFacet.collectRedemption(0);
 
    }
 
```
Scenario 3: mintDollar after an amoMinterBorrow.


```solidity
import "forge-std/StdError.sol";

    function testMintDollarAfterAmoBorrow_ShouldRevert()
        public
    {
         vm.prank(admin);
        ubiquityPoolFacet.setPriceThresholds(
            1000000, // mint threshold
            1000000 // redeem threshold
        );

        // user sends 50 collateral tokens and gets 49 Dollars (-1% mint fee)
        vm.prank(user);
        ubiquityPoolFacet.mintDollar(
            0, // collateral index
            50e18, // Dollar amount
            49e18, // min amount of Dollars to mint
            50e18 // max collateral to send
        );

        // user redeems 99 Dollars for 97.02 (accounts for 2% redemption fee) collateral tokens
        vm.prank(user);

        ubiquityPoolFacet.redeemDollar(
            0, // collateral index
            49e18, // Dollar amount
            40e18 // min collateral out
        );

        // amoMinter borrows the maximum he is allowed.
        uint256 balanceContract = MockERC20(collateralToken).balanceOf(address(ubiquityPoolFacet));
        vm.prank(address(dollarAmoMinter));
        ubiquityPoolFacet.amoMinterBorrow(balanceContract); 

        // user tries to mint additional dollars, but this reverts due to underflow. 
        vm.prank(user);
        vm.expectRevert(stdError.arithmeticError);
        ubiquityPoolFacet.mintDollar(
            0, // collateral index
            20e18, // Dollar amount
            19e18, // min amount of Dollars to mint
            20e18 // max collateral to send
        );
    }


```

Partial mitigation: `freeCollateralBalance`

Implementing a check to limit the amoMinterBorrow to the freeCollateralBalance is what is suggested by the NatSpec of the freeCollateralBalance function. However this does not fix scenario 1, as shown in the test below. 


```solidity
    function testFreeCollateralBalanceForMaximumAmoMint_ShouldRevert()
        public
    {
        vm.prank(admin);
        ubiquityPoolFacet.setPriceThresholds(
            1000000, // mint threshold
            1000000 // redeem threshold
        );

        // user sends 100 collateral tokens and gets 99 Dollars (-1% mint fee)
        vm.prank(user);
        ubiquityPoolFacet.mintDollar(
            0, // collateral index
            100e18, // Dollar amount
            99e18, // min amount of Dollars to mint
            100e18 // max collateral to send
        );


        // amoMinter borrows the maximum he is allowed. Which now is the balance - unclaimedCollateral
        uint256 freeCollateralBalance = ubiquityPoolFacet.freeCollateralBalance(0);
        vm.prank(address(dollarAmoMinter));
        ubiquityPoolFacet.amoMinterBorrow(freeCollateralBalance);

        // user tries to redeem her dollars, but this still reverts. This is because the only collateral left in the contract is already claimed by other users. 
        vm.prank(user);
        vm.expectRevert("Insufficient pool collateral");
        ubiquityPoolFacet.redeemDollar(
            0, // collateral index
            99e18, // Dollar amount
            90e18 // min collateral out
        );
    }
 
```


## Impact

Both the minting and redeeming function are broken and unlucky users also lose their Dollars without being able to get their collateral back. 


## Code Snippet

https://github.com/sherlock-audit/2023-12-ubiquity/blob/main/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibUbiquityPool.sol#L399-L465
https://github.com/sherlock-audit/2023-12-ubiquity/blob/main/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibUbiquityPool.sol#L268-L276
https://github.com/sherlock-audit/2023-12-ubiquity/blob/main/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibUbiquityPool.sol#L326-L386
https://github.com/sherlock-audit/2023-12-ubiquity/blob/main/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibUbiquityPool.sol#L574-L598

## Tool used

Manual Review, Foundry

## Recommendation

The obvious mitigation is to implement the below require in the amoMinterBorrow function, as is suggested by the NatSpec of the freeCollateralBalance function

```solidity

        require(
            collateralAmount <= freeCollateralBalance(minterCollateralIndex), "Collateral exceeds maximum amount allowed"
        );
```

However, this does not fix scenario 1,  as is shown in the last test. 

One solution is to further limit the amo borrow at launch to a small percentage of the freeCollateralBalance, which can be increased over time when the collateral collected from fees becomes a large enough buffer to ensure liquidity.  
