Savory Sandstone Eagle

high

# AMO Minter borrowing while there are unclaimed collateral redemptions bricks the pool

## Summary
The `UbiquityPoolFacet` allows trusted parties called AMO minters to borrow unrestricted amount of particular collateral provided to the pool.  Borrowing happens through the `amoMinterBorrow` method:

https://github.com/sherlock-audit/2023-12-ubiquity/blob/main/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibUbiquityPool.sol#L574

The problem is that the above function doesn't take into consideration the amount of collateral that was redeemed, but not collected yet - the so called `freeCollateralBalance` which can break inner accounting logic and make minting/redeeming for that collateral inoperable.

## Vulnerability Detail
The problem arises from the fact that the AMO minters are allowed to borrow the whole `collateral.balanceOf(pool)`, including redeemed tokens that should be collected by their owners. 

The first and more mild impact of this is that AMO minters can borrow a bigger part of the collateral in the pool and prevent the users from rightfully collecting their tokens:

```solidity
// @notice Used to collect collateral tokens after redeeming/burning Ubiquity Dollars
function collectRedemption(
        uint256 collateralIndex
    ) internal returns (uint256 collateralAmount) {
       .....

        if (
            poolStorage.redeemCollateralBalances[msg.sender][collateralIndex] >
            0
        ) {
            collateralAmount = poolStorage.redeemCollateralBalances[msg.sender][
                collateralIndex
            ];
         .....
        }

        // @audit - this will fail because not enough `collateralAmount ` is available if AMO borrows too much collateral
        if (sendCollateral) {
            IERC20(poolStorage.collateralAddresses[collateralIndex])
                .safeTransfer(msg.sender, collateralAmount);
        }
}
```
But there is another more serious problem that arises when AMO minters borrow more than the amount  of `unclaimedPoolCollateral[collateralIndex]`. This breaks the calculations inside `LibUbiquityPool.freeCollateralBalance()`:

```solidity
function freeCollateralBalance(
        uint256 collateralIndex
    ) internal view returns (uint256) {
        UbiquityPoolStorage storage poolStorage = ubiquityPoolStorage();
        return
            IERC20(poolStorage.collateralAddresses[collateralIndex])
                .balanceOf(address(this))
                .sub(poolStorage.unclaimedPoolCollateral[collateralIndex]);
    }
```
As you can see the amount of unclaimed tokens is deducted from the total balance of the pool. But when AMO minter borrows more than the amount accumulated as `unclaimedPoolCollateral` we get an overflow error.

`unclaimedPoolCollateral` is used in both `redeemDollar` & `mintDollar` functions, which means all minting/redeeming in the pool for that collateral will not work:

```solidity
 function mintDollar(
        uint256 collateralIndex,
        uint256 dollarAmount,// based on logic below should be 1e6
        uint256 dollarOutMin,
        uint256 maxCollateralIn
    )
        internal
        collateralEnabled(collateralIndex)
        returns (uint256 totalDollarMint, uint256 collateralNeeded)
    {
       ........
        require(
            freeCollateralBalance(collateralIndex).add(collateralNeeded) <=    <------------------------
                poolStorage.poolCeilings[collateralIndex],
            "Pool ceiling"
        );

       .....
    }
```

```solidity
function redeemDollar(
        uint256 collateralIndex,
        uint256 dollarAmount,
        uint256 collateralOutMin
    )
        internal
        collateralEnabled(collateralIndex)
        returns (uint256 collateralOut)
    {
       .........

        // checks
        require(
            collateralOut <=
                (IERC20(poolStorage.collateralAddresses[collateralIndex]))     <------------- same as calling freeCollateralBalance
                    .balanceOf(address(this))
                    .sub(poolStorage.unclaimedPoolCollateral[collateralIndex]),
            "Insufficient pool collateral"
        );
        require(collateralOut >= collateralOutMin, "Collateral slippage");

     ......
    }
```

### POC

You can add the test to `UbiquityPoolFacet.t.sol` and run `forge test --contracts test/diamond/facets/UbiquityPoolFacet.t.sol --mt testAmoMinterBorrow_GetsUnclaimedCollateral`
```solidity
function testAmoMinterBorrow_GetsUnclaimedCollateral() public {
        vm.startPrank(admin);
        ubiquityPoolFacet.setPriceThresholds(
            1000000, // mint threshold
            1000000 // redeem threshold
        );
        //set fees to 0 to make the POC simpler
        ubiquityPoolFacet.setFees(0, 0, 0);
        vm.stopPrank();

        // user sends 100 collateral tokens and gets 100 Dollars
        vm.prank(user);
        ubiquityPoolFacet.mintDollar(
            0, // collateral index
            100e18, // Dollar amount
            99e18, // min amount of Dollars to mint
            100e18 // max collateral to send
        );

        // user redeems 99 Dollars for 99 collateral tokens
        vm.prank(user);
        uint256 collateralOut= ubiquityPoolFacet.redeemDollar(
            0, // collateral index
            99e18, // Dollar amount
            90e18 // min collateral out
        );

        // redemption blocktime passes
        vm.roll(10);

        uint256 freeCollateral = ubiquityPoolFacet.freeCollateralBalance(
            0
        );

        // pool has 100 dollars
        assertEq(collateralToken.balanceOf(address(ubiquityPoolFacet)),  100e18);
        // but only 1 dollar is free after 99 was marked for redemption
        assertEq(freeCollateral, 1e18);


        // amoMinter borrows 99 dollars(collateralOut), even though only 1 are free
        vm.prank(address(dollarAmoMinter));
        ubiquityPoolFacet.amoMinterBorrow(collateralOut);

        assertEq(collateralToken.balanceOf(address(dollarAmoMinter)), collateralOut);
        // pool is left with 1 dollars after amoBorrow
        assertEq(collateralToken.balanceOf(address(ubiquityPoolFacet)),  1e18);

        // freeCollateralBalance() fails 
        // balanceOf(pool) - unclaimedPoolCollateral -> 1 - 99
        vm.expectRevert(); //throws "arithmetic underflow or overflow"
        ubiquityPoolFacet.freeCollateralBalance(
            0
        );

        // minting is no longer working for that collateral
        vm.expectRevert(); //throws "arithmetic underflow or overflow"
        ubiquityPoolFacet.mintDollar(
            0, // collateral index
            100e18, // Dollar amount
            99e18, // min amount of Dollars to mint
            100e18 // max collateral to send
        );

        // redeeming is also bricked
        vm.expectRevert(); //throws "arithmetic underflow or overflow"
        ubiquityPoolFacet.redeemDollar(
            0, // collateral index
            99e18, // Dollar amount
            90e18 // min collateral out
        );

         // user is unable to collect redemption
        vm.startPrank(user);
        vm.expectRevert("ERC20: transfer amount exceeds balance");
        ubiquityPoolFacet.collectRedemption(
            0 // collateral index
        );
    }

```

## Impact

The impact is two fold:

- Users that have successfully redeemed dollars, cannot collect their collateral
- The pool becomes inoperable for that collateral

## Code Snippet
https://github.com/sherlock-audit/2023-12-ubiquity/blob/main/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibUbiquityPool.sol#L574

https://github.com/sherlock-audit/2023-12-ubiquity/blob/main/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibUbiquityPool.sol#L268

## Tool used

Manual Review

## Recommendation
Restrict the AMO minter borrows up to the available `freeCollateralBalance`:
```solidity
 function amoMinterBorrow(uint256 collateralAmount) internal onlyAmoMinter {
 
  //add this check
   require(
              freeCollateralBalance(minterCollateralIndex) >= collateralAmount,
              "Not enough free collateral"
          );

................

}


}
```
