Mini Lemon Frog

medium

# `amoMinterBorrow` may block users from claiming their collateral

# Web3 Security Report

## Summary

This issue is related to the `amoMinterBorrow` function, where there is a lack of a check for free collateral balance present in the pool. This could potentially lead to a scenario where an AMO Minter borrowing collateral may block other users from claiming their collateral (for a long time), disrupting the normal operation of the withdrawal process in the pool. 

## Vulnerability Detail

In the Ubiquity pool the collateral withdrawal process is done in two step : first users call the `redeemDollar` function to indicate that they are withdrawing their deposited collateral this will increment the `unclaimedPoolCollateral` for the collateral being withdrawn to indicate that those funds are not available in the pool anymore. After some delay `redemptionDelayBlocks` users will be allowed to call the `collectRedemption()` function to get their collateral back.

The `freeCollateralBalance` is used to determine the actual collateral balance available in the pool: 

```solidity
function freeCollateralBalance(
        uint256 collateralIndex
) internal view returns (uint256) {
        UbiquityPoolStorage storage poolStorage = ubiquityPoolStorage();
        return
            IERC20(poolStorage.collateralAddresses[collateralIndex])
                .balanceOf(address(this))
                .sub(poolStorage.unclaimedPoolCollateral[collateralIndex]);
}
```

The issue now arises in the `amoMinterBorrow` function which allows AMO Minters to borrow collateral from the pool, but it lacks a critical check for the free collateral balance present in the pool. This omission means that an AMO Minter could potentially borrow collateral without ensuring that enough collateral is available to fulfill the unclaimed collateral. As a result, other users attempting to claim their collateral by calling `collectRedemption()` might face insufficiencies and will not be able to get their collateral back until the funds are returned (which may take some time), this leads to a disruption in the expected withdrawal behavior of the pool.

## Impact

This vulnerability may lead to a temporary denial of service (DOS) for users attempting to claim their collateral as the funds where borrowed by AMO Minters without checking free collateral balance present in the pool. 

## Code Snippet

https://github.com/sherlock-audit/2023-12-ubiquity/blob/main/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibUbiquityPool.sol#L568-L598

```solidity
/**
 * @notice Allows AMO minters to borrow collateral to make yield in external
 * protocols like Compound, Curve, erc...
 * @dev Bypasses the gassy mint->redeem cycle for AMOs to borrow collateral
 * @param collateralAmount Amount of collateral to borrow
 */
function amoMinterBorrow(uint256 collateralAmount) internal onlyAmoMinter {
    UbiquityPoolStorage storage poolStorage = ubiquityPoolStorage();

    // checks the collateral index of the minter as an additional safety check
    uint256 minterCollateralIndex = IDollarAmoMinter(msg.sender)
        .collateralIndex();

    // checks to see if borrowing is paused
    require(
        poolStorage.isBorrowPaused[minterCollateralIndex] == false,
        "Borrowing is paused"
    );

    // ensure collateral is enabled
    require(
        poolStorage.isCollateralEnabled[
            poolStorage.collateralAddresses[minterCollateralIndex]
        ],
        "Collateral disabled"
    );

    //@audit there is no check for the free collateral balance in the pool
    //@audit so when AMOMinter borrows collateral it could temporarily block (DOS) some users from claiming their collateral

    // transfer
    IERC20(poolStorage.collateralAddresses[minterCollateralIndex])
        .safeTransfer(msg.sender, collateralAmount);
}
```

## Tool used
Manual Review

## Recommendation

To mitigate the risk associated with this vulnerability, it is recommended to implement a check for free collateral balance before executing the collateral transfer in the `amoMinterBorrow` function. This check should resemble the one present in the `redeemDollar` function, ensuring that collateral can be claimed after the borrow operation. 

```solidity
/**
 * @notice Allows AMO minters to borrow collateral to make yield in external
 * protocols like Compound, Curve, erc...
 * @dev Bypasses the gassy mint->redeem cycle for AMOs to borrow collateral
 * @param collateralAmount Amount of collateral to borrow
 */
function amoMinterBorrow(uint256 collateralAmount) internal onlyAmoMinter {
    UbiquityPoolStorage storage poolStorage = ubiquityPoolStorage();

    // checks the collateral index of the minter as an additional safety check
    uint256 minterCollateralIndex = IDollarAmoMinter(msg.sender)
        .collateralIndex();

    // checks to see if borrowing is paused
    require(
        poolStorage.isBorrowPaused[minterCollateralIndex] == false,
        "Borrowing is paused"
    );

    // ensure collateral is enabled
    require(
        poolStorage.isCollateralEnabled[
            poolStorage.collateralAddresses[minterCollateralIndex]
        ],
        "Collateral disabled"
    );

    //@audit check for the free collateral balance present in the pool
    require(
            collateralAmount <= freeCollateralBalance(collateralIndex),
            "Insufficient pool collateral"
        );

    // Missing Check for Free Collateral Balance

    // transfer
    IERC20(poolStorage.collateralAddresses[minterCollateralIndex])
        .safeTransfer(msg.sender, collateralAmount);
}
```