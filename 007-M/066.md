Kind Orchid Stork

medium

# Users can get double incentives by transferring to themselves

## Summary
Users get incentives from `UbiquityDollarToken` transfers. Both transfer functionality and the `_checkAndApplyIncentives()` functions do not have a self-transfer check. Users can get double incentives without actually transferring anything.

## Vulnerability Detail
Users get incentives from `UbiquityDollarToken` transfers and this is performed with `_checkAndApplyIncentives()` function.  
[https://github.com/sherlock-audit/2023-12-ubiquity/blob/main/ubiquity-dollar/packages/contracts/src/dollar/core/UbiquityDollarToken.sol#L84C14-L84C38](https://github.com/sherlock-audit/2023-12-ubiquity/blob/main/ubiquity-dollar/packages/contracts/src/dollar/core/UbiquityDollarToken.sol#L84C14-L84C38)

```solidity
    function _checkAndApplyIncentives(
        address sender,
        address recipient,
        uint256 amount
    ) internal {
        // incentive on sender
-->     address senderIncentive = incentiveContract[sender]; 
        if (senderIncentive != address(0)) {
            IIncentive(senderIncentive).incentivize(
                sender,
                recipient,
                _msgSender(),
                amount
            );
        }

        // incentive on recipient
-->     address recipientIncentive = incentiveContract[recipient]; //@audit there is no check whether sender & recipient is the same. 
        if (recipientIncentive != address(0)) {
            IIncentive(recipientIncentive).incentivize(
                sender,
                recipient,
                _msgSender(),
                amount
            );
        }

        // incentive on operator
        address operatorIncentive = incentiveContract[_msgSender()];
        if (
-->         _msgSender() != sender &&
-->         _msgSender() != recipient && //@audit there is a check regarding to operator.
            operatorIncentive != address(0)
        ) {
            IIncentive(operatorIncentive).incentivize(
                sender,
                recipient,
                _msgSender(),
                amount
            );
        }
        
        // rest of the code.
    }
```

As we can see above, this function gets the incentive addresses for both recipient and the sender, and directly incentivises them without checking if they are the same.

I would like to point out that there is a check for operators. If the operator is the sender or the recipient, operator incentive is not paid. The same check should be there for users too. If the `sender == recipient`, there should be no incentive. However, users will be incentivised twice if the `sender == recipient`.

By the way, the `_transfer` function does not have an input check other than zero address checks. `from` can be equal to `to`.

```solidity
function _transfer(address from, address to, uint256 amount) internal virtual {
        require(from != address(0), "ERC20: transfer from the zero address");
        require(to != address(0), "ERC20: transfer to the zero address");

        _beforeTokenTransfer(from, to, amount);

        uint256 fromBalance = _balances[from];
        require(fromBalance >= amount, "ERC20: transfer amount exceeds balance");
        unchecked {
            _balances[from] = fromBalance - amount;
            // Overflow not possible: the sum of all balances is capped by totalSupply, and the sum is preserved by
            // decrementing then incrementing.
            _balances[to] += amount;
        }

        emit Transfer(from, to, amount);

        _afterTokenTransfer(from, to, amount);
    }
```

## Impact
Double incentives will be paid without any actual transfer.

## Code Snippet
https://github.com/sherlock-audit/2023-12-ubiquity/blob/main/ubiquity-dollar/packages/contracts/src/dollar/core/UbiquityDollarToken.sol#L84C5-L136C6

## Tool used

Manual Review

## Recommendation
I would recommend checking `sender != recipient` in the `_checkAndApplyIncentives()` function.