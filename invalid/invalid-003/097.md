Savory Sandstone Eagle

high

# ChainLink Price feed in Ubiquity pool can be set for the wrong collateral token

## Summary
In order to mint dollars in the Ubiquity pool against  a collateral, first it has to be added (`addCollateralToken`). Every new collateral that is added has an associated chainlink price oracle address used to fetch its price during minting & redeeming. The `UbiquityPool` facet also exposes an admin restricted function  `setCollateralChainLinkPriceFeed` used for updating the chainlink address.

Since no validation is made in the function if the provided collateral address has actually been added to the pool, if a non-registered address is passed the provided `chainLinkPriceFeedAddress` will always update the `collateralAddress` at index `0`.

## Vulnerability Detail
As you can see from the snippet below, no check is made if the provided `collateralAddress` parameter has previously been added to the pool. This means the mapping `poolStorage.collateralIndex` will return the default value `0` for all non-set values (all non-registered collateral addresses).

SInce the call will not revert, this will always update the price feed for collateral at zero 0

```solidity
 /**
     * @notice Sets collateral ChainLink price feed params
     * @param collateralAddress Collateral token address
     * @param chainLinkPriceFeedAddress ChainLink price feed address
     * @param stalenessThreshold Threshold in seconds when chainlink answer should be considered stale
     */
    function setCollateralChainLinkPriceFeed(
        address collateralAddress,
        address chainLinkPriceFeedAddress,
        uint256 stalenessThreshold
    ) internal {
        UbiquityPoolStorage storage poolStorage = ubiquityPoolStorage();

        //@audit  if I put non-registered address, I update the index for 0

        // can I change other collateral
        uint256 collateralIndex = poolStorage.collateralIndex[
            collateralAddress
        ];

        // set price feed address
        poolStorage.collateralPriceFeedAddresses[
            collateralIndex
        ] = chainLinkPriceFeedAddress;

        // set staleness threshold in seconds when chainlink answer should be considered stale
        poolStorage.collateralPriceFeedStalenessThresholds[
            collateralIndex
        ] = stalenessThreshold;

        emit CollateralPriceFeedSet(
            collateralIndex,
            chainLinkPriceFeedAddress,
            stalenessThreshold
        );
    }
```

Even though this method  is restricted only to the admin, this does not make it safe in any way.

Considering the impact that the above bug might have on the pool, it should not be allowed!

### POC
I've coded a POC to demonstrate the issue. Imagine  the scenario:
- DAI is the only collateral in the system (living at index 0)
- an admin has the task to set the feed for AAVE (he misses the fact that it was not yet added)
- the admin calls `setCollateralChainLinkPriceFeed` 
- this sets the chainlink feed of DAI to use the feed for AAVE
- now DAI costs x10 more - at the time of writing DAI is around 1$ & AAVE around 10$

Add to `UbiquityPoolFacet.t.sol` & run `forge test --contracts test/diamond/facets/UbiquityPoolFacet.t.sol --mt test_setWrongFeed`:

```solidity
function test_setWrongFeed() public {
        address AAVE_token = vm.addr(50);
        address newPriceFeed = vm.addr(100);

        //ensure collateral is not registered
        vm.expectRevert("Invalid collateral");
        LibUbiquityPool.CollateralInformation memory info = ubiquityPoolFacet
            .collateralInformation(AAVE_token);

        vm.prank(admin);
         // set price feed for non-existent token
        ubiquityPoolFacet.setCollateralChainLinkPriceFeed(
            address(AAVE_token), // collateral token address
            address(newPriceFeed), // price feed address
            1 days // price feed staleness threshold in seconds
        );

        //the original collateral was actually updated
        info = ubiquityPoolFacet
            .collateralInformation(address(collateralToken));
        assertEq(info.collateralPriceFeedAddress, newPriceFeed);
    }
```

## Impact
Setting the wrong price feed for a collateral can severely affect the exchange ratios when minting/redeeming in the system. Especially if the price difference is substantial. Like in the example above for DAI & AAVE. This in turn can lead to losses both to the protocol or the users.

## Code Snippet
https://github.com/sherlock-audit/2023-12-ubiquity/blob/main/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibUbiquityPool.sol#L707-L708

## Tool used

Manual Review

## Recommendation

Consider adding a check in the beginning of the function if the collateral was previously added to the system. For example you can check if an oracle address has already been set (when adding collateral the oracle address is always provided). Another option is to use the `collateralEnabled` modifier.