Able Chocolate Puppy

medium

# A user may lose his funds during redeem process by administrator's pausing redeem.

## Summary
User burns his ubiquity dollar tokens first during redeem process, but the corresponding collaterals can only be collected after `redemptionDelayBlock`.
During this period, if the administrator pauses redeem, the user will not be able to collect collaterals for a long time or forever.
Since the user has already paid ubiquity dollar tokens, it is wrong to lock the corresponding collaterals and not return them to the user.

## Vulnerability Detail
`LibUbiquityPool.sol#redeemDollar` function is following.
```solidity
    function redeemDollar(
        uint256 collateralIndex,
        uint256 dollarAmount,
        uint256 collateralOutMin
    )
        internal
        collateralEnabled(collateralIndex)
        returns (uint256 collateralOut)
    {
        UbiquityPoolStorage storage poolStorage = ubiquityPoolStorage();

        require(
            poolStorage.isRedeemPaused[collateralIndex] == false,
            "Redeeming is paused"
        );

        // update Dollar price from Curve's Dollar Metapool
        LibTWAPOracle.update();
        // prevent unnecessary redemptions that could adversely affect the Dollar price
        require(
            getDollarPriceUsd() <= poolStorage.redeemPriceThreshold,
            "Dollar price too high"
        );

        uint256 dollarAfterFee = dollarAmount
            .mul(
                UBIQUITY_POOL_PRICE_PRECISION.sub(
                    poolStorage.redemptionFee[collateralIndex]
                )
            )
            .div(UBIQUITY_POOL_PRICE_PRECISION);

        // update collateral price
        updateChainLinkCollateralPrice(collateralIndex);

        // get collateral output for incoming Dollars
        collateralOut = getDollarInCollateral(collateralIndex, dollarAfterFee);

        // checks
        require(
            collateralOut <=
                (IERC20(poolStorage.collateralAddresses[collateralIndex]))
                    .balanceOf(address(this))
                    .sub(poolStorage.unclaimedPoolCollateral[collateralIndex]),
            "Insufficient pool collateral"
        );
        require(collateralOut >= collateralOutMin, "Collateral slippage");

        // account for the redeem delay
448:    poolStorage.redeemCollateralBalances[msg.sender][
            collateralIndex
        ] = poolStorage
        .redeemCollateralBalances[msg.sender][collateralIndex].add(
                collateralOut
            );
        poolStorage.unclaimedPoolCollateral[collateralIndex] = poolStorage
            .unclaimedPoolCollateral[collateralIndex]
            .add(collateralOut);

        poolStorage.lastRedeemedBlock[msg.sender] = block.number;

        // burn Dollars
        IERC20Ubiquity ubiquityDollarToken = IERC20Ubiquity(
            LibAppStorage.appStorage().dollarTokenAddress
        );
464:    ubiquityDollarToken.burnFrom(msg.sender, dollarAmount);
    }
```
As can be seen, `L464` burns user's `ubiquityDollarToken` but not return the corresponding collaterals and only records the amount of collaterals in `L448`.
User has to collect them by calling `collectRedemption` after `redemptionDelayBlocks`.

`LibUbiquityPool.sol#collectRedemption` function is following.
```solidity
    function collectRedemption(
        uint256 collateralIndex
    ) internal returns (uint256 collateralAmount) {
        UbiquityPoolStorage storage poolStorage = ubiquityPoolStorage();

        require(
482:        poolStorage.isRedeemPaused[collateralIndex] == false,
            "Redeeming is paused"
        );
        require(
            (
487:            poolStorage.lastRedeemedBlock[msg.sender].add(
                    poolStorage.redemptionDelayBlocks
                )
            ) <= block.number,
            "Too soon to collect redemption"
        );

        bool sendCollateral = false;

        if (
            poolStorage.redeemCollateralBalances[msg.sender][collateralIndex] >
            0
        ) {
            collateralAmount = poolStorage.redeemCollateralBalances[msg.sender][
                collateralIndex
            ];
            poolStorage.redeemCollateralBalances[msg.sender][
                collateralIndex
            ] = 0;
            poolStorage.unclaimedPoolCollateral[collateralIndex] = poolStorage
                .unclaimedPoolCollateral[collateralIndex]
                .sub(collateralAmount);
            sendCollateral = true;
        }

        // send out the tokens
        if (sendCollateral) {
            IERC20(poolStorage.collateralAddresses[collateralIndex])
                .safeTransfer(msg.sender, collateralAmount);
        }
    }
```
`L487` forces the user to call the above function after `redemptionDelayBlocks` of calling `redeemDollar` function.
But `L482` will be reverted when `isRedeemPaused` is true.

Therefore, the following case is available.
1. User calls `redeemDollar` function to redeem 1000$ of ubiquity dollar token and collect corresponding USDCs.
  At this time, the user's 1000$ of ubiquity dollar tokens will be burnt immediately.
2. Administrator calls `toggleMintRedeemBorrow` function and pause the redeem of USDC.
3. After that, the user calls `collectRedemption` function to collect the unclaimed USDCs after `redemptionDelayBlocks`.
   By `L482`, the function will be reverted.
4. The user paid 1000$ of ubiquity dollar tokens but cannot collect the corresponding USDCs for a long time.

## Impact
The condition of `L487` is not for punishing a particular user.
Nevertheless, a user will not collect his collaterals for long periods or forever in the redeem process and the collaterals will be locked in the contract.

## Code Snippet
https://github.com/sherlock-audit/2023-12-ubiquity/blob/main/ubiquity-dollar/packages/contracts/src/dollar/libraries/LibUbiquityPool.sol#L481-L484

## Tool used
Manual Review

## Recommendation
Remove the condition of `L482` from `collectRedemption` function as follows.
```solidity
    function collectRedemption(
        uint256 collateralIndex
    ) internal returns (uint256 collateralAmount) {
        UbiquityPoolStorage storage poolStorage = ubiquityPoolStorage();

--      require(
--          poolStorage.isRedeemPaused[collateralIndex] == false,
--          "Redeeming is paused"
--      );
        require(
            (
                poolStorage.lastRedeemedBlock[msg.sender].add(
                    poolStorage.redemptionDelayBlocks
                )
            ) <= block.number,
            "Too soon to collect redemption"
        );

        bool sendCollateral = false;

        if (
            poolStorage.redeemCollateralBalances[msg.sender][collateralIndex] >
            0
        ) {
            collateralAmount = poolStorage.redeemCollateralBalances[msg.sender][
                collateralIndex
            ];
            poolStorage.redeemCollateralBalances[msg.sender][
                collateralIndex
            ] = 0;
            poolStorage.unclaimedPoolCollateral[collateralIndex] = poolStorage
                .unclaimedPoolCollateral[collateralIndex]
                .sub(collateralAmount);
            sendCollateral = true;
        }

        // send out the tokens
        if (sendCollateral) {
            IERC20(poolStorage.collateralAddresses[collateralIndex])
                .safeTransfer(msg.sender, collateralAmount);
        }
    }
```